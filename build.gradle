plugins {
    id "java"
    id "net.fabricmc.fabric-loom-remap" version "${loom_version}"
    id "io.freefair.lombok" version "9.1.0"
    id "com.diffplug.spotless" version "8.1.0"
    id "idea"
    id "signing"
    id "maven-publish"
}

def ENV = System.getenv()
def runNumber = ENV.GITHUB_RUN_NUMBER == null ? "9999" : ENV.GITHUB_RUN_NUMBER
def isRelease = project.hasProperty("release")

version = isRelease ? "${project.mod_version}" : "${project.mod_version}-build.${runNumber}"

group = project.maven_group
base.archivesName = project.archives_base_name

repositories {
    mavenCentral()

    maven {url = 'https://maven.parchmentmc.org' }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {url = "https://mvn.devos.one/snapshots" }
    maven {url = "https://maven.terraformersmc.com/" }
    maven { url = "https://jitpack.io"}
}

sourceSets {
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }
}

loom {
    accessWidenerPath = file("src/main/resources/codebebelib.accesswidener")

    runs {
        testmodClient {
            client()
            name = "Testmod Client"
            source sourceSets.testmod
        }
        testmodServer {
            server()
            name = "Testmod Server"
            source sourceSets.testmod
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include modImplementation("com.github.spotbugs:spotbugs-annotations:${project.spotbugs_annotations}")
    include modImplementation("io.github.fabricators_of_create:Porting-Lib:${project.porting_lib_version}")
    include modImplementation("net.covers1624:Quack:${project.quack_version}")
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes}")

    modLocalRuntime("com.terraformersmc:modmenu:${project.modmenu_version}") {
        exclude group: "net.fabricmc", module: "fabric-loader"
    }

    testmodImplementation sourceSets.main.output
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            loader_version         : loader_version,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
    withJavadocJar()
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name.toLowerCase()
            version = project.version

            from components.java
        }
    }

    repositories {
        if (isRelease) {
            maven {
                url "https://maven.tobynguyen.dev/releases"
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        } else {
            maven {
                url "https://maven.tobynguyen.dev/snapshots"
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}

signing {}

spotless {
    encoding "UTF-8"

    java {
        target "src/main/java/**/*.java", "src/test/java/**/*.java"

        toggleOffOn()

        removeUnusedImports('cleanthat-javaparser-unnecessaryimport')
        endWithNewline()
        palantirJavaFormat()
    }

    json {
        target("src/*/resources/**/*.json")
        targetExclude("src/generated/resources/**")

        biome("2.3.7").downloadDir(new File(rootDir, ".gradle/biome").absolutePath).configPath(new File(rootDir, "spotless/biome.json").absolutePath)

        endWithNewline()
    }

    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
